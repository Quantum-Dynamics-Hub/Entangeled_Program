#
set terminal pngcairo font "arial,24" size 800, 600 enhanced rounded truecolor

set lmargin at screen 0.17
set rmargin at screen 0.95
set bmargin at screen 0.15
set tmargin at screen 0.95

#set xtics 1000.0
#set xrange[0:1]
#set yrange[-0.25:0.125]
set key spacing 1.0 font ",24"
set key horizontal 

# color definitions
set style line 11 lc rgb '#8b1a0e' pt 1 ps 1 lt 1 lw 5 # --- red
set style line 12 lc rgb '#FF4500' pt 6 ps 1 lt 1 lw 5 # --- orangered
set style line 13 lc rgb '#B22222' pt 6 ps 1 lt 1 lw 5 # --- firebrick
set style line 14 lc rgb '#DC143C' pt 6 ps 1 lt 1 lw 5 # --- crimson

set style line 21 lc rgb '#5e9c36' pt 6 ps 1 lt 1 lw 5 # --- green
set style line 22 lc rgb '#006400' pt 6 ps 1 lt 1 lw 5 # --- darkgreen
set style line 23 lc rgb '#228B22' pt 6 ps 1 lt 1 lw 5 # --- forestgreen
set style line 24 lc rgb '#808000' pt 6 ps 1 lt 1 lw 5 # --- olive
set style line 25 lc rgb '#00FF10' pt 6 ps 1 lt 1 lw 5 # --- lime

set style line 31 lc rgb '#8A2BE2' pt 6 ps 1 lt 1 lw 5 # --- blueviolet
set style line 32 lc rgb '#000E8B' pt 6 ps 1 lt 1 lw 5 # --- royalblue
set style line 33 lc rgb '#00008B' pt 6 ps 1 lt 1 lw 5 # --- darkblue

set style line 41 lc rgb '#2F4F4F' pt 6 ps 1 lt 1 lw 5 # --- darkslategray
set style line 42 lc rgb '#C0C0C0' pt 6 ps 1 lt 1 lw 5 # --- silver
set style line 43 lc rgb '#D2691E' pt 6 ps 1 lt 1 lw 5 # --- chocolate


set output "Energy_vs_Time.png"
set xlabel "Time (ps)" offset 0.0, 0.5
set ylabel  "Energy (a.u)" offset 1.3, 0.0

plot  "energy/energy.txt" u ($1/41341.0):3 w l lt 7 lw 4 t "Potential",\
      "energy/energy.txt" u ($1/41341.0):2 w l lt 6 lw 4 t "Kinetic",\
      "energy/energy.txt" u ($1/41341.0):4 w l lt 8 lw 4 t "Total"


set output "Q[i]_vs_P[i].png"
set xlabel "Position (a.u)" offset 0.0, 0.5
set ylabel "Momentum (a.u)" offset 1.3, 0.0

fName = 'phase_space/phase_space.txt'
stat fName nooutput
N = STATS_columns #number of columns found in file
plot for [i=2:N/2] fName u 2*i-1:2*i w l lt 8 lw 1 t ""


set output "Q[i]_vs_t.png"
set xlabel "Time (ps)" offset 0.0, 0.5
set ylabel "Position (a.u)" offset 1.3, 0.0

fName = 'pos_space/pos_space.txt'
stat fName nooutput
N = STATS_columns #number of columns found in file

plot for [i=2:N] fName u ($1/41341.0):i w l lt 8 lw 1 t ""



set output "tunnel.png"
set xlabel "Time (a.u)"  offset 0.0, 0.5
set ylabel "Population on initial State" offset 1.3, 0.0

plot  "tunnel/tunnel.txt" u ($1):2 w l lt 8 lw 7 t ""


set output "perturbation.png"
set xlabel "Time (ps)"  offset 0.0, 0.5
set ylabel "Perturbation Term" offset 2.0, 0.0

plot  "perturbation/perturbation.txt" u ($1/41341.0):2 w l lt 8 lw 5 t ""


